plugins {
	id "org.springframework.boot" version "3.2.2"
	id "io.spring.dependency-management" version "1.1.4"
	id "java"
	id "checkstyle"
	id "com.github.spotbugs" version "6.0.9"
	id "io.freefair.lombok" version "6.6.3"
	id "jacoco"
}

apply plugin: "java"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "checkstyle"
apply plugin: "com.github.spotbugs"

group = "uk.nhs.adaptors"
sourceCompatibility = "17"

repositories {
	mavenCentral()
}

ext {
	set("springCloudVersion", "2021.0.9")
}

dependencies {
	implementation ("org.springframework.boot:spring-boot-starter-actuator") {
		exclude group: "org.eclipse.jetty", module: "jetty-webapp"
	}
	implementation("org.springframework.cloud:spring-cloud-starter-gateway") {
		exclude group: "io.netty", module: "netty-codec-http2"
	}
	implementation "io.netty:netty-codec-http2:4.1.61.Final"

	implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:5.4.0"

	implementation "com.heroku.sdk:env-keystore:1.1.6"

	implementation ("org.apache.commons:commons-lang3:3.14.0")

	implementation "org.eclipse.jetty:jetty-webapp:9.4.43.v20210629"

	// Test
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "com.jayway.jsonpath:json-path:2.9.0"
	testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
	testImplementation "com.github.tomakehurst:wiremock:2.27.2"
	testImplementation "uk.org.webcompere:system-stubs-jupiter:1.2.0"
	testImplementation "org.assertj:assertj-core:3.25.3"
	testImplementation "org.testcontainers:testcontainers:1.15.3"

	constraints {
		implementation('org.bouncycastle:bcprov-jdk15on:1.68') {
			because 'https://snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-1052448'
		}
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file("src/intTest/java")
		}

		resources {
			srcDir file("src/intTest/resources")
		}
	}
}

configurations {
	integrationTestCompileOnly.extendsFrom testCompileOnly
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntime.extendsFrom testRuntime
	integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

task integrationTest(type: Test) {
	useJUnitPlatform() {
		description = "Runs integration tests."
		group = "verification"

		testClassesDirs = sourceSets.integrationTest.output.classesDirs
		classpath = sourceSets.integrationTest.runtimeClasspath
		shouldRunAfter test
	}
}

tasks.withType(Test) {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

check.dependsOn integrationTest
jacocoTestReport.dependsOn integrationTest

spotbugsTest.enabled = false
spotbugsIntegrationTest.enabled = false
spotbugsMain {
	reports {
		html {
			enabled = true
		}
		xml {
			enabled = true
		}
	}
}